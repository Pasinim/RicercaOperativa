! Esercizio Schedule
! 
! Variabili: x(i)= quantità prodotto i=1..3 [ettolitri/giorno]
!            s(i), e(i) = istanti di inizio e fine della produzione i=1..3 [ore]
! Le variabili sono continue e non-negative

! Funzione obiettivo: massimizzazione dei ricavi di vendita [Euro/giorno]
max 1000 x1 + 800 x2 + 750 x3

st

! Vincoli tecnologici sulla produzione [ettolitri/giorno]
mat_A) 0.90 x1 + 0.05 x2 + 0.05 x3 <= 6.4
mat_B) 0.05 x1 + 0.90 x2 + 0.05 x3 <= 3.0
mat_C) 0.05 x1 + 0.05 x2 + 0.90 x3 <= 2.8

! Vincoli sui limiti temporali alle produzioni [ore]
min_s1) s1 >= 0
max_s1) s1 <= 8
min_e1) e1 >= 4
max_e1) e1 <=12
min_s2) s2 >= 6
max_s2) s2 <=14
min_e2) e2 >=10
max_e2) e2 <=18
min_s3) s3 >=12
max_s3) s3 <=20
min_e3) e3 >=16
max_e3) e3 <=24

! Vincoli di non sovrapposizione tra le produzioni [ore]

ord_s1e1) s1 - e1 <= 0
ord_e1s2) e1 - s2 <= 0
ord_s2e2) s2 - e2 <= 0
ord_e2s3) e2 - s3 <= 0
ord_s3e3) s3 - e3 <= 0

! Vincoli sulla velocità di produzione [ettolitri/giorno]

prod_1) 0.6 e1 - 0.6 s1 - x1 >= 0
prod_2) 0.6 e2 - 0.6 s2 - x2 >= 0
prod_3) 0.6 e3 - 0.6 s3 - x3 >= 0

end
